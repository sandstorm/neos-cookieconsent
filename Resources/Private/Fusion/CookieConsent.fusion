prototype(Sandstorm.CookieConsent:Component.CookieConsent) < prototype(Neos.Fusion:Component) {
    # Should already be converted when passed, e.g. through privacyPolicyHref.@process.convert = Neos.Neos:ConvertUris
    privacyPolicyHref = null
    cookieModalTranslations = null
    language = null
    apps = null
    includeLibraryCss = true
    # If the cookieconsent popup should open automatically as popup on this page. Can be used to disable the popup on the
    # privacy policy page to enable the user to read the privacy policy before consenting. Why? On mobile the popup is
    # probably (almost) fullscreen and can only be closed by taking a decision. But the decision must be informed,
    # so reading of the privacy policy should be possible.
    disableAutoPopup = false

    renderer = afx`
        <Sandstorm.CookieConsent:Klaro.Settings
            privacyPolicyHref={props.privacyPolicyHref}
            language={props.language}
            cookieModalTranslations={props.cookieModalTranslations}
            apps={props.apps}
        />
        <Sandstorm.CookieConsent:Script.ConvertStringToRegex />
        <Sandstorm.CookieConsent:Script.KlaroJs disableAutoPopup={props.disableAutoPopup} />
        <Sandstorm.CookieConsent:Script.KlaroCss @if.true={props.includeLibraryCss} />
    `
}
